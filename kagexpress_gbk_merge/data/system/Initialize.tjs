// Initialize.tjs - システムの初期化
// Copyright (C)2001-2008, W.Dee and contributors  改?配布は自由です

// システムバ`ジョン
var kagVersion = "3.30 stable rev.2";

/*
	Debug.message へのショ`トカット
*/

var dm = Debug.message; // これで dm("message"); でコンソ`ルに message を表示できる


/*
	デフォルトの「捕捉されない例外」ハンドラ
*/

System.exceptionHandler = function (e)
{
	// どこにも捕捉されない例外がシステム趣遣蹲饯丹欷龊稀このv数が
	// 呼ばれる。e は例外オブジェクト。
	if(e instanceof "ConductorException")
	{
		// コンダクタの投げた例外の龊
		Debug.logAsError(); // ログのファイルへのき出し幼鳏伍_始など
		var event_disabled = System.eventDisabled;
		System.eventDisabled = true;
			// エラ`の理由を表示させているgにイベントがk生すると
			// やっかいなのでいったんイベントk生を停止させる
		System.inform(e.message);
		System.eventDisabled = event_disabled;
			// イベントをk生するかどうかを元の状Bに
		return true; // true を返すと本体趣抢外のI理は行わなくなる
	}
	else
	{
		return false; // false を返すと通常の例外I理
	}
};


/*
	パスのO定
	後に指定した物が、より先されて使用される
*/


function useArchiveIfExists(name)
{
	// name が存在していたらそのア`カイブを使う
	var arcname;
	if(Storages.isExistentStorage(arcname = System.exePath + name))
		Storages.addAutoPath(arcname + ">");
}

Storages.addAutoPath(System.exePath + "video/"); // exePath 以下の video/
Storages.addAutoPath("video/"); // video フォルダ
Storages.addAutoPath("others/"); // その他
Storages.addAutoPath("rule/"); // ル`ル画像フォルダ
Storages.addAutoPath("sound/"); // 抗音フォルダ
Storages.addAutoPath("bgm/"); // BGM フォルダ
Storages.addAutoPath("fgimage/"); // 前景画像フォルダ
Storages.addAutoPath("bgimage/"); // 背景画像フォルダ
Storages.addAutoPath("scenario/"); // シナリオフォルダ
Storages.addAutoPath("image/"); // そのほかの画像フォルダ
Storages.addAutoPath("system/"); // システムフォルダ

// パッチア`カイブの仕鳏仁褂
// もしこれらの名前を持ったア`カイブがg行可能ファイルと
// 同じ鏊にあった龊稀それを先して使う
useArchiveIfExists("video.xp3");
useArchiveIfExists("others.xp3");
useArchiveIfExists("rule.xp3");
useArchiveIfExists("sound.xp3");
useArchiveIfExists("bgm.xp3");
useArchiveIfExists("fgimage.xp3");
useArchiveIfExists("bgimage.xp3");
useArchiveIfExists("scenario.xp3");
useArchiveIfExists("image.xp3");
useArchiveIfExists("system.xp3");

useArchiveIfExists("patch.xp3");

// 追加のパッチ用ア`カイブの仕
for(var i = 2; ; i++)
{
	// パッチ用ア`カイブ patch2.xp3, patch3.xp3 ... がある龊悉悉饯沥椁
	// 先してiみzむように
	if(Storages.isExistentStorage(System.exePath + "patch" + i + ".xp3"))
		Storages.addAutoPath(System.exePath + "patch" + i + ".xp3>");
	else
		break;
}

delete useArchiveIfExists; // useArchiveIfExists は使いKわったので一晗しておく

/*
	システムバ`ジョン
*/
Debug.notice("OS : " + System.osName + " (" + System.platformName + ")");
Debug.notice("KAG : " + kagVersion);
Debug.notice("Kirikiri : " + System.versionString);

/*
	( デバッグ ) rgy
*/

var parseStartTick = System.getTickCount();


/*
	スクリプトiみzみラッパ`
*/

function KAGLoadScript(name)
{
	var start = System.getTickCount();
	Scripts.execStorage(name);
	dm(name + " をiみzみました(" + (System.getTickCount() - start) + "ms)");
}

var loaded_scripts = %[];

function KAGLoadScriptOnce(name)
{
	// 指定したスクリプトをiみzむが、一回しかiみzまない
	if(global.loaded_scripts[name] === true) return; // 既にiみzんでいる
	global.KAGLoadScript(name);
	global.loaded_scripts[name] = true;
}

/*
	Config.tjs iみzみ
*/
if(Storages.isExistentStorage("Config.tjs"))
{
	KAGLoadScript("Config.tjs");
}
else if(Storages.isExistentStorage("Config.~new"))
{
	System.inform("Config.tjs がつかりません。\nsystem フォルダにある "
		"Config.~new ファイルを Config.tjs に改名してください。");
	System.exit();
}
else
{
	throw new Exception("Config.tjs がつかりません。");
}

/*
	Config.tjs バ`ジョンチェック
*/

if(typeof global.config_version == "undefined" || config_version != kagVersion)
{
	KAGLoadScript("UpdateConfig.tjs");
}

/*
	二重起婴违隶Д氓
*/

// g行可能ファイルのパスをキ`にしてロックを行う
if(!System.createAppLock(System.exePath.replace(/[^A-Za-z]/g, '_')))
{
	// すでに起婴筏皮い
	System.inform(System.title + "はすでに起婴筏皮い蓼");
	System.exit();
}


/*
	オン?デマンド?ロ`ディングを行うための定x
*/


property askYesNo { getter() { KAGLoadScript("YesNoDialog.tjs"); return global.askYesNo; } }
property CheckBoxLayer { getter() { KAGLoadScript("CheckBoxLayer.tjs"); return global.CheckBoxLayer; } }
property ButtonLayer { getter() { KAGLoadScript("ButtonLayer.tjs"); return global.ButtonLayer; } }
property EditLayer { getter() { KAGLoadScript("EditLayer.tjs"); return global.EditLayer; } }
property KAGPlugin { getter() { KAGLoadScript("Plugin.tjs"); return global.KAGPlugin; } }

/*
	各システムiみzみ
*/
dm("KAG System スクリプトをiみzんでいます...");

KAGLoadScript("Utils.tjs");
KAGLoadScript("KAGLayer.tjs");
KAGLoadScript("HistoryLayer.tjs");
KAGLoadScript("BGM.tjs");
KAGLoadScript("SE.tjs");
KAGLoadScript("Movie.tjs");
KAGLoadScript("Conductor.tjs");
KAGLoadScript("AnimationLayer.tjs");
KAGLoadScript("GraphicLayer.tjs");
KAGLoadScript("MessageLayer.tjs");
KAGLoadScript("Menus.tjs");
KAGLoadScript("DefaultMover.tjs");
KAGLoadScript("MainWindow.tjs");
if(Storages.isExistentStorage("Override.tjs"))
	KAGLoadScript("Override.tjs");
if(Storages.isExistentStorage(System.exePath + "Override2.tjs"))
	KAGLoadScript(System.exePath + "Override2.tjs");


/*
	( デバッグ ) rgy
*/
dm("スクリプトのiみzみに " + (System.getTickCount() - parseStartTick) + "ms かかりました");
parseStartTick = System.getTickCount();

/*
	( デバッグ ) VM コ`ドのダンプ
*/

// Scripts.dump();

/*
	( デバッグ ) rgy
*/

parseStartTick = System.getTickCount();


/*
	KAG メインウィンドウの作成
	グロ`バルメンバ kag が存在しなければ KAGWindow クラスの
	オブジェクトを作成して入れる
*/

global.kag = new KAGWindow() if typeof global.kag == "undefined";




/*
	グロ`バルからgにアクセスできるように、いくつかの
	涫のe名を作成
*/

var f = kag.flags;   // ユ`ザ涫 (フラグ)
var sf = kag.sflags; // システム涫 (システム)
var tf = kag.tflags; // 一r涫 (一rフラグ)

property mp
{
	getter { return kag.conductor.macroParams; }
}

/*
	( デバッグ ) rgy
*/
dm("KAGMainWindow のコンストラクタで " + (System.getTickCount() - parseStartTick) + "ms かかりました");
delete parseStartTick;


/*
	AfterInit.tjs が存在すればg行
*/
if(Storages.isExistentStorage("AfterInit.tjs"))
	KAGLoadScript("AfterInit.tjs");
if(Storages.isExistentStorage(System.exePath + "AfterInit2.tjs"))
	KAGLoadScript(System.exePath + "AfterInit2.tjs");

/*
	コマンドラインパラメ`タとして -ovr が指定されていれば
	そのパラメ`タを TJS 式としてg行
*/
{
	var ovr = System.getArgument('-ovr');
	if(ovr !== void && ovr != 'yes') Scripts.eval(ovr);
}

/*
	first.ks のg行
*/

kag.process("first.ks");

