// ButtonLayer.tjs - ボタンレイヤ
// Copyright (C)2001-2008, W.Dee and contributors  改?配布は自由です

/*
ボタンとして幼鳏工毳欹ぅ浃扦埂
押しzまれたrの画像、マウスカ`ソルがレイヤ内にあるときの画像、通常の画像
をそれぞれ浃à毪长趣できます。

式  new ButtonLayer(<window>, <parent>)

<window> : このレイヤを作成するウィンドウ
<parent> : Hレイヤ

*/


class ButtonLayer extends KAGLayer
{
	var Butt_imageLoaded = false; // 画像がiみzまれたか
	var Butt_mouseOn = false; // レイヤ内にマウスがあるか
	var Butt_mouseDown = false; // マウスボタンが押されているか
	var Butt_color = clNone;
	var Butt_caption = ''; // ボタンのキャプション
	var Butt_captionColor = 0x000000; // キャプションの色
	var Butt_keyPressed = false;
	var Butt_showFocusImage = false;

	function ButtonLayer(win, par)
	{
		super.KAGLayer(win, par);

		if(typeof win.cursorPointed !== "undefined")
			cursor = win.cursorPointed;

		hitType = htMask;
		hitThreshold = 0;
		focusable = true; // フォ`カスを得られる
	}

	function finalize()
	{
		super.finalize(...);
	}

	function assign(src)
	{
		// src の情螭颏长违堀骏螗欹ぅ浃衰偿冤`
		assignImages(src, true);
		Butt_imageLoaded = src.Butt_imageLoaded;
		Butt_color = src.Butt_color;
		Butt_caption = src.Butt_caption;
		Butt_captionColor = src.Butt_captionColor;
		hitThreshold = src.hitThreshold;
		update();
	}

	function drawState(s)
	{
		// 状B s に辘工牖像を描画
		// s :  0 : 普通の状B
		//      1 : ボタンが押された状B
		//      2 : ボタンの上にマウスカ`ソルがある状B
		//     (3): フォ`カスがある龊
		if(!enabled)
		{
			s = 0; // o孔B
		}

		if(Butt_imageLoaded)
		{
			// ボタンイメ`ジがiみzまれている
			// TODO: keyboard focus
			imageLeft = -s * width;
		}
		else
		{
			if(Butt_keyPressed) s = 1; // 押されている

			// とキャプションを描画
			// クリア
			face = dfAlpha;
			colorRect(0, 0, width, height, 0, -255);
			// 下地をTる
			if(Butt_color != clNone)
				colorRect(0, 0, width, height, Butt_color, 128);
			// 文字のサイズを得る
			var tw, th;
			tw = font.getTextWidth(Butt_caption);
			th = font.getTextHeight(Butt_caption);
			if(s == 0 || s == 2)
			{
				// 通常あるいはマウスが上にある
				colorRect(0, 0, width, 1, 0xffffff, 128);
				colorRect(0, 1, 1, height-2, 0xffffff, 128);
				colorRect(width-1, 1, 1, height-1, 0x000000, 128);
				colorRect(1, height-1, width-2, 1, 0x000000, 128);
				drawText((width-tw)>>1, (height-th)>>1, 
					Butt_caption, Butt_captionColor, nodeEnabled?255:128);
			}
			else
			{
				// 押されている
				colorRect(0, 0, width, 1, 0x000000, 128);
				colorRect(0, 1, 1, height-2, 0x000000, 128);
				colorRect(width-1, 1, 1, height-1, 0xffffff, 128);
				colorRect(1, height-1, width-2, 1, 0xffffff, 128);
				drawText(((width-tw)>>1) +1, ((height-th)>>1) +1, 
					Butt_caption, Butt_captionColor, nodeEnabled?255:128);
			}

			if(s != 0)
				colorRect(2, 2, width-4, height-4, clHighlight, 64); // ハイライトする

			if(focused)
			{
				// フォ`カスがあるのでハイライトする
				colorRect(2, 2, width-4, 1, clHighlight, 128);
				colorRect(2, 3, 1, height-5, clHighlight, 128);
				colorRect(3, height-3, width-5, 1, clHighlight, 128);
				colorRect(width-3, 3, 1, height-6, clHighlight, 128);
			}
		}
	}

	function loadImages(storage, key)
	{
		// 画像をiみzむ
		super.loadImages(storage, key);
		super.width = imageWidth \ (Butt_showFocusImage ? 4 : 3);
		super.height = imageHeight;
		callOnPaint = true;
		Butt_imageLoaded = true;
	}

	function discardImage()
	{
		// 画像を破し、文字ボタンレイヤとして幼鳏工毪瑜Δ摔工
		Butt_imageLoaded = false;
		imageLeft = imageTop = 0;
		update();
	}

	function onExecute(x, y, button, shift)
	{
		// マウスボタンが押されて、放されたときに呼ばれる
		// 何かこのボタンが押されたことによりコマンドなどをg装したい龊悉
		// onMouseUp や onClick をオ`バ`ライドするよりも
		// このメソッドをオ`バ`ライドすることを推Xする
	}

	function onMouseDown()
	{
		// onMouseDown イベントハンドラ
		Butt_mouseDown = true;
		focus();
		update();
		super.onMouseDown(...);
	}

	function onMouseUp()
	{
		// onMouseUp イベントハンドラ
		if(Butt_mouseDown) onExecute(...);
		Butt_mouseDown = false;
		update();
		super.onMouseUp(...);
	}

	function onClick()
	{
		// onClick イベントハンドラ
		super.onClick(...);
	}

	function draw()
	{
		// F在の状Bにあわせて描画を行う
		if(Butt_mouseDown) drawState(1);
		else if(Butt_mouseOn) drawState(2);
		else if(Butt_showFocusImage && focused) drawState(3);
		else drawState(0);
	}

	function onPaint()
	{
		// 描画の直前に呼ばれる
		super.onPaint(...);
		draw();
	}

	function onMouseEnter()
	{
		// マウスがレイヤI域内に入った
		update();
		Butt_mouseOn = true;
		super.onMouseEnter(...);
	}

	function onMouseLeave()
	{
		// マウスがレイヤI域から出ていった
		update();
		Butt_mouseOn = false;
		Butt_mouseDown = false;
		super.onMouseLeave(...);
	}

	function onNodeDisabled()
	{
		// レイヤのノ`ドが不可になった
		super.onNodeDisabled(...);
		Butt_mouseDown = false;
		update();
	}

	function onNodeEnabled()
	{
		// レイヤのノ`ドが有郡摔胜盲
		super.onNodeEnabled(...);
		update();
	}

	function onFocus()
	{
		// フォ`カスを得た
		super.onFocus(...);
		update();
	}

	function onBlur()
	{
		// フォ`カスを失った
		super.onBlur(...);
		Butt_mouseDown = false;
		update();
	}

	function onKeyDown(key, shift, process)
	{
		// キ`が押された
		if(process)
		{
			if(key == VK_RETURN || key == VK_SPACE)
			{
				// スペ`スキ`またはエンタ`キ`
				Butt_keyPressed = true;
				update();
				super.onKeyDown(key, shift, false); // I理をしたのでprocessにfalseを渡す
			}
			else
			{
				super.onKeyDown(...);
			}
		}
		else
		{
			// process が false の龊悉I理は行わない
			super.onKeyDown(...);
		}
	}

	function onKeyUp(key, shift, process)
	{
		// キ`がxされた
		if(process)
		{
			if(key == VK_RETURN || key == VK_SPACE)
			{
				// スペ`スキ`またはエンタ`キ`
				var flag = Butt_keyPressed;
				Butt_keyPressed = false;
				update();
				super.onKeyUp(key, shift, false);
				if(flag) onClick(width \ 2, height \ 2); // クリック幼
			}
			else
			{
				super.onKeyUp(...);
			}
		}
		else
		{
			super.onKeyUp(...);
		}
	}

	function onKeyPress(key, shift)
	{
		super.onKeyPress(...);
	}


	property width
	{
		setter(x)
		{
			super.width = x;
			imageWidth = x;
			Butt_imageLoaded = false;
			update();
		}
		getter
		{
			return super.width;
		}
	}

	property height
	{
		setter(x)
		{
			super.height = x;
			imageHeight = x;
			Butt_imageLoaded = false;
			update();
		}
		getter
		{
			return super.height;
		}
	}

	function setSize(w, h)
	{
		super.setSize(w, h);
		setImageSize(w, h);
		Butt_imageLoaded = false;
		update();
	}

	property caption
	{
		setter(x)
		{
			Butt_caption = x;
			update();
		}
		getter
		{
			return Butt_caption;
		}
	}

	property color
	{
		setter(x)
		{
			Butt_color = int x;
			update();
		}
		getter
		{
			return Butt_color;
		}
	}

	property captionColor
	{
		setter(x)
		{
			Butt_captionColor = int x;
			update();
		}
		getter
		{
			return Butt_captionColor;
		}
	}

	property showFocusImage
	{
		setter(x) { Butt_showFocusImage = x; }
		getter { return Butt_showFocusImage; }
	}
}


