@call storage="snow.ks"
; ↑ 雪 plug-in をiみzむ
@wait time=200

@eval exp="f.bg=''"
; ↑F在iみzまれている背景をしている

; マクロ changebg_and_clear の定x
@macro name="changebg_and_clear"
@if exp="mp.storage != f.bg"
; ↑既にiみzまれている背景と同じならば切り替えI理は行わない
@eval exp="f.bg = mp.storage"
@backlay
@layopt layer=message0 page=back visible=false
@trans method=crossfade time=300
@wt
@image * layer=base page=back
@trans method=crossfade time=300
@wt
@cm
@layopt layer=message0 page=back visible=true
@trans method=crossfade time=300
@wt
@endif
@endmacro

; マクロ loadbg の定x
@macro name="loadbg"
@image * layer=base
@eval exp="f.bg = mp.storage"
@endmacro

; マクロ start_select の定x
@macro name="start_select"
@backlay
@nowait
@history output=false
@current page=back
@endmacro

; マクロ end_select の定x
@macro name="end_select"
@trans method=crossfade time=400
@wt
@endnowait
@history output=true
@current page=fore
@endmacro

*syokai_start|吉里吉里とKAGのB介 - メニュ`
@startanchor

; 背景に画像をiみzみ、メッセ`ジレイヤにメニュ`を描画
@backlay
@loadbg storage="_24_5" page=back
@current page=back
@cm
@layopt layer=message0 page=back visible=true
@nowait
@history output=false
@style align=center
[font size=40 color=0x00ffff]吉里吉里とKAGのB介[resetfont][r]
[r]
[link target="*about_kirikiri"]吉里吉里とは[endlink][r]
[link target="*about_kag"]KAGとは[endlink][r]
[r]
[r]
[r]
[r]
[r]
[r]
[r]
[link exp="kag.shutdown()" color=0xff0000 hint="吉里吉里/KAGのB介をK了します"]K了[endlink]
@endnowait
@history output=true
@current page=fore

; メッセ`ジレイヤのトランジション
@trans method=crossfade time=800
@wt

; 通^h
@record

; xk肢がxkされるまで停止
@s

*to_syokai_start
; syokai_start にる
@backlay
@layopt layer=message0 page=back visible=false
@trans method=crossfade time=300
@wt
@jump target=*syokai_start


*about_kirikiri|吉里吉里とは
@changebg_and_clear storage="_24_4"
　吉里吉里は、TJSというスクリプト言Zを使っていろいろな事をするためのソフトウェアです。[l][r]
　TJSはJavaとJavaScriptを足して３で割ったような言Zで、CやC++に比べれば得しやすい言Zだと思います。[l][r]
　吉里吉里ではこのTJSで本体を制御することにより、さまざまなアプリケ`ションを作成することができます。[l][r]
　特にマルチメディア系のC能がく、比^的静的な表Fを用いる２Ｄゲ`ムにmしています。[p]
*about_kirikiri2|
@cm
　吉里吉里は、レイヤと呼ばれる画面を何枚も重ね合わせて画面を成します。[l]レイヤはアルファブレンドによる重ね合わせが可能で、A造を瘠毪长趣猡扦ます。[l][r]
　レイヤには胜PNG/JPEG/ERI/BMPをiみzみ可能で、Susie-pluginでiみzみ可能な形式をすることもできます。[l][r]
　描画はあまり}jなことはできませんが、半透明矩形の描画やアンチエイリアス可能な文字表示、画像の大s小や湫韦蛐肖κ陇できます。[l][r]
　AVI/MPEGやSWF(Macromedia Flash)をム`ビ`として再生させることができます。[p]
*about_kirikiri3|
@cm
　吉里吉里ではCD-DA、MIDIシ`ケンスデ`タ、PCMを再生させることができ、それぞれ音量{が可能です。[l]PCMはoRsの.WAVファイルのほか、プラグインで再生可能な形式をでき、OggVorbisも再生することができます。[l][r]
　PCMは}数を同rに再生することができます。[l]CD-DAやMIDIシ`ケンスデ`タでもo理矢理やろうと思えば}数同r再生できます。[p]
*about_kirikiri4
@cm
　その他、周xツ`ルとして、
}数のファイルを一つにまとめたり、g体でg行可能なファイルを作成することができる[font color=0xffff00]Releaser[resetfont]、[l]
吉里吉里本体のO定を行う[font color=0xffff00]吉里吉里O定[resetfont]、[l]
制作者趣钎榨━螗趣蛴靡猡贰プレ`ヤ趣衰榨━螗趣インスト`ルされてoくても使えるようにする[font color=0xffff00]レンダリングgみフォント作成ツ`ル[resetfont]、[l]
透明度を持った画像フォ`マットgの相互Qを行う[font color=0xffff00]透^画像フォ`マットコンバ`タ[resetfont]があります。[l]
[r]
[r]
@start_select
[link target=*to_syokai_start]メニュ`にる[endlink]
@end_select
[s]

*about_kag|KAGとは
@changebg_and_clear storage="_24_4"
　KAGは、ビジ○アルノベルやサウンドノベルのようなノベル系ゲ`ムや、xk肢をxんでスト`リ`がMむような文字ベ`スのアドベンチャ`ゲ`ムを作成するためのキットです。[l][r]
　KAGは吉里吉里をゲ`ムエンジンとして幼鳏丹护毪郡幛违攻リプトで、それ自体はTJSスクリプトでかれています。[l]KAG用のスクリプトは「シナリオ」と呼ばれ、TJSスクリプトとはまたeのものです。[l]TJSスクリプトはプログラミングの知Rがかなり必要になりますが、シナリオはよりgで述しやすいものです。[l][r]
　KAGは吉里吉里の上に成り立つシステムなので、吉里吉里のC能のほとんどはKAGで使用できます。[p]
*about_kag3|
@cm
　KAGの文字表示は、ごEの通りのアンチエイリアス文字表示に加え、[l][r]
[font size=60]大きな文字[resetfont]を表示したり、[l][r]
[ruby text="る"]ル[ruby text="び"]ビ[ruby text="を"]を[ruby text="ふ"]振[ruby text="っ"]っ[ruby text="た"]た[ruby text="り"]り、[l][font shadow=false edge=true edgecolor=0xff0000]F取り文字にしたり[resetfont]、[l][r]
[style align=center]センタリングしてみたり[r]
[style align=right]右めしてみたり[r][resetstyle]
[l]
[graph storage="ExQuestion.png" alt="!?"]のような特殊号を表示してみたり、[l][r]
と、いろいろできます。[p]
*about_kag4|
@position vertical=true
　また、kき表示をすることもできます。[l][r]
　kきでも横きと全く同じC能を使用することができます。[p]
@layopt layer=message0 visible=false
@layopt layer=message1 visible=true
@current layer=message1
@position frame=messageframe left=20 top=280 marginl=16 margint=16 marginr=0 marginb=16 draggable=true vertical=false
　このようにメッセ`ジの中にメッセ`ジを表示させることもできます。[l]アドベンチャ`ゲ`ムで良くあるタイプです。[p]
@layopt layer=message1 visible=false
@layopt layer=message0 visible=true
@current layer=message0
@position vertical=false
*about_kag5|
@cm
　立ち}はこのように(あいかわらず[ruby text="?"]う[ruby text="?"]にですみません)
@backlay
@image storage=uni page=back layer=0 visible=true opacity=255
@trans method=crossfade time=1000
@wt
アルファブレンドによる重ね合わせが可能です。[l][r]
　このように
@backlay
@layopt page=back layer=0 opacity=128
@trans method=crossfade time=1000
@wt
薄く表示することもできます。[l][r]
　胜巫Bで３つまで重ね合わせて表示できます。[p]
@backlay
@layopt page=back layer=0 visible=false
@trans method=crossfade time=300
@wt
*about_kag6|
@cm
　トランジション(画面切り替え)には胜牵长膜畏Nがあります。[l][r]
　一つはgなクロスフェ`ド、[l]
@backlay
@layopt page=back layer=message0 visible=false
@trans method=crossfade time=300
@wt
@backlay
@image storage="_24" page=back layer=base
@trans method=crossfade time=3000
@wt
@backlay
@image storage="_24_4" page=back layer=base
@trans method=crossfade time=3000
@wt
@backlay
@layopt page=back layer=message0 visible=true
@trans method=crossfade time=300
@wt
[l][r]
　もう一つはスクロ`ル抗を出すことのできるスクロ`ルトランジション、[l]
@backlay
@layopt page=back layer=message0 visible=false
@trans method=crossfade time=300
@wt
@backlay
@image storage="_24" page=back layer=base
@trans method=scroll from=right stay=stayfore time=3000
@wt
@backlay
@image storage="_24_4" page=back layer=base
@trans method=scroll from=bottom stay=nostay time=3000
@wt
@backlay
@layopt page=back layer=message0 visible=true
@trans method=crossfade time=300
@wt
[l][r]
　そして最後は制作者が自由にパタ`ンを作成できるユニバ`サルトランジションです。[l][r]
　ユニバ`サルトランジションはル`ル画像と呼ばれるグレ`スケ`ルの画像を用意し、その画像の暗いところからより早く切り替えが始まるものです。[l][r]
　たとえば、[l]
@image layer=base page=fore storage="trans1"
このようなル`ル画像であれば???[l]
@backlay
@layopt page=back layer=message0 visible=false
@image storage="_24_4" page=back layer=base
@trans method=crossfade time=300
@wt
@backlay
@image storage="_24" page=back layer=base
@trans method=universal rule="trans1" vague=64 time=3000
@wt
@backlay
@image storage="_24_4" page=back layer=base
@trans method=universal rule="trans1" vague=64 time=3000
@wt
@backlay
@layopt page=back layer=message0 visible=true
@trans method=crossfade time=300
@wt
[r]
　たとえば、[l]
@image layer=base page=fore storage="nami"
このようなル`ル画像であれば???[l]
@backlay
@layopt page=back layer=message0 visible=false
@image storage="_24_4" page=back layer=base
@trans method=crossfade time=300
@wt
@backlay
@image storage="_24" page=back layer=base
@trans method=universal rule="nami" vague=64 time=3000
@wt
@backlay
@image storage="_24_4" page=back layer=base
@trans method=universal rule="nami" vague=64 time=3000
@wt
@backlay
@layopt page=back layer=message0 visible=true
@trans method=crossfade time=300
@wt
[r]
　という感じで、いろいろ作ることができます。[p]
*about_kag7|KAGとは
@cm
　BGMとしてはCD-DA、MIDI、PCMのいずれかを使用できます。[l]抗音にはPCMを使用できます。[l]いずれもフェ`ドなどの音量制御ができます。[l][r]
　PCMは胜oRsの.WAVを再生できます。[l]また、プラグインで再生可能な形式をでき、OggVorbisも再生できます。[l][r]
　ム`ビ`はAVI/MPEG/SWFを再生できます。[p]
*about_kag8|
@cm
　KAGの涫は文字列でも数でも入れることができ、涫の数はo制限、文字列のLさもo制限、数は整数だけでなくg数もQうことができます。[l]これはKAGの涫の仕というか、KAGのベ`スとなっているTJSの仕です。[l][r]
　涫にはゲ`ム涫とシステム涫の２Nあって、ゲ`ム涫は荬趣趣猡苏iみzまれたり保存されたりしますが、システム涫は荬趣祥vSなく、常に同じ内容を保つことができるものです。[l][r]
　涫を使った例を示します???[p]
@eval exp="f.v1 = intrandom(1, 9)"
@eval exp="f.v2 = intrandom(1, 9)"
@eval exp="f.ans = f.v1 * f.v2"
@eval exp="f.input = ''"
*about_kag_var_0|算}
@cm
　算}です。[emb exp="f.v1"] × [emb exp="f.v2"] は？[r]
[font size=20](下の入力冥巳肓Δ筏郡椤よこの「OK」をクリックしてください)[resetfont][r]
[r]
@start_select
　[edit name="f.input" length=200 opacity=80 bgcolor=0x000000 color=0xffffff] [link target=*about_kag_var_1]　　　OK　　　[endlink][r]
[r]
　[link target=*about_kag_9]面倒なのでとばす[endlink]
@end_select
@eval exp="kag.fore.messages[0].links[0].object.focus()"
; ↑入力冥衰榨┅`カスをO定する
; 「システム - 前にる」でこの位置にれるようにここで通^hを行う
@record
[s]

*about_kag_var_1
@commit
@jump cond="str2num(f.input) == f.ans" target=*about_kag_var_correct
@cm
　不正解！[l][r]
　もう一度入力してください。[p]
@jump target=*about_kag_var_0

*about_kag_var_correct
@cm
　正解です！[p]
@jump target=*about_kag_9

*about_kag_9|
@cm
@snowinit forevisible=true backvisible=false
　KAGの大きな特栅趣筏啤その高い性とカスタマイズ性がい菠椁欷蓼埂[l]KAGだけではgFできないようなC能も、TJSを使って直接吉里吉里を制御すればいろいろな事ができます。[l][r]
　たとえば、KAG用プラグインとして「雪」を表示させるプラグインをiみzめば、このように雪を表示させることができます。[l]ほかにもトランジションのNをやすプラグインなどがあります。[l][r]
　また、KAGそのものがTJSスクリプトでかれているため、スクリプトを涓すれば隅々にわたって幼鳏颔スタマイズする事ができます。[p]
@backlay
@snowopt backvisible=false
@trans method=crossfade time=1000
@wt
@snowuninit
*about_kag_fin|KAGのB介おしまい
@cm
　KAGのB介はこれでおしまいです。[l][r]
　みなさんも是非吉里吉里/KAGを使ってすばらしいゲ`ムを作ってください。[l][r]
[r]
@start_select
[link exp="System.shellExecute('http://www.piass.com/kpc/')" hint="吉里吉里/KAG推M委T会を_きます"]吉里吉里/KAG推M委T会[endlink][r]
[link exp="System.shellExecute('http://kikyou.info/tvp/')" hint="ダウンロ`ドペ`ジを_きます"]吉里吉里ダウンロ`ドペ`ジ[endlink][r]
[r]
[link target=*to_syokai_start]メニュ`にる[endlink]
@end_select
[s]
